/*
 * Copyright 2019 Jan Slany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'com.jfrog.bintray'

sourceSets {
	// Code which does not need to be compiled in build
	aux
}

eclipse {
	project.name = 'Music'
}

repositories {
	mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

task printVersion {
	group 'Help'
	description 'Displays the project version'
	doLast {
		println version
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		pluginPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}

/*
 * The "bintrayUser" and "bintrayKey" properites must be set.
 * This can be done either in the user's $GRADLE_USER_HOME/gradle.properties
 * file or directly when invoking gradle (e.g. -PbintrayUser=someuser).
 */
bintray {
	user = bintrayUser
	key = bintrayKey
	pkg {
		repo = 'maven'
		name = project.name
		desc = project.description
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/Singond/Gradle-Pandoc.git'
		issueTrackerUrl = 'https://github.com/Singond/Gradle-Pandoc/issues'
		labels = ['gradle-plugin', 'pandoc']
		githubRepo = 'Singond/Gradle-Pandoc'
//		githubReadme = 'README.md'  // Not supported
//		githubReleaseNotesFile = ''
		version {
			name = project.version
			desc = "Version ${project.version}"
			released = new Date()   // today
			vcsTag = "v${project.version}"
		}
	}
	publications = ['pluginPublication']
	publish = true                  // Publish automatically upon upload
}
