/*
 * Copyright 2019 Jan Slany
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'eclipse'

sourceSets {
	// Code which does not need to be compiled in build
	aux
}

eclipse {
	project.name = 'Music'
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'junit:junit:4.12'
}

task printVersion {
	group 'Help'
	description 'Displays the project version'
	doLast {
		println version
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

test.testLogging {
//	events "passed", "skipped", "failed"
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			pom {
				name = 'Music'
				description = project.description
				url = 'https://github.com/Singond/Music'

				scm {
					connection = 'scm:git:git://github.com/Singond/Music.git'
					developerConnection = 'scm:git:ssh://github.com/Singond/Music.git'
					url = 'https://github.com/Singond/Music/tree/master'
				}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'Singond'
						name = 'Singon'
						email = 'singond@seznam.cz'
					}
				}
			}
		}
	}
	repositories {
		maven {
			credentials {
				username = sonatypeUser
				password = sonatypePassword
			}
			url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
		}
	}
}

signing {
	useGpgCmd()  // use gpg-agent to obtain keys
	sign configurations.archives
	sign publishing.publications.maven
}
